/*
 * This software is in the public domain under CC0 1.0 Universal plus a
 * Grant of Patent License.
 *
 * To the extent possible under law, the author(s) have dedicated all
 * copyright and related and neighboring rights to this software to the
 * public domain worldwide. This software is distributed without any
 * warranty.
 *
 * You should have received a copy of the CC0 Public Domain Dedication
 * along with this software (see the LICENSE.md file). If not, see
 * <http://creativecommons.org/publicdomain/zero/1.0/>.
 */

tasks.withType(JavaCompile) { options.compilerArgs << "-Xlint:unchecked" }
tasks.withType(JavaCompile) { options.compilerArgs << "-Xlint:deprecation" }
tasks.withType(GroovyCompile) { options.compilerArgs << "-Xlint:unchecked" }
tasks.withType(GroovyCompile) { options.compilerArgs << "-Xlint:deprecation" }

version = '3.0.0-rc2'

apply plugin: 'groovy'
apply plugin: 'war'
// to run gradle-versions-plugin use "gradle dependencyUpdates"
apply plugin: 'com.github.ben-manes.versions'
// uncomment to add the Error Prone compiler; not enabled by default (doesn't work on Travis CI)
// apply plugin: 'net.ltgt.errorprone'
buildscript {
    repositories {
        jcenter()
        // uncomment to add the Error Prone compiler: maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.28.0'
        // uncomment to add the Error Prone compiler: classpath 'net.ltgt.gradle:gradle-errorprone-plugin:0.0.8'
    }
}
dependencyUpdates.resolutionStrategy = { componentSelection { rules -> rules.all { ComponentSelection selection ->
    boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'b'].any { qualifier -> selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-].*/ }
    if (rejected) selection.reject('Release candidate')
} } }

// NOTE: packages should match group, name, version found at https://bintray.com/bintray/jcenter
repositories {
    flatDir name: 'localLib', dirs: projectDir.absolutePath + '/lib'
    jcenter() // http://jcenter.bintray.com
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'
archivesBaseName = 'moqui'

sourceSets {
    start
    execWar
}

groovydoc {
    docTitle = "Moqui Framework ${version}"
    source = sourceSets.main.allSource
}

// Log4J has annotation processors, disable to avoid warning
tasks.withType(JavaCompile) { options.compilerArgs << "-proc:none" }
tasks.withType(GroovyCompile) { options.compilerArgs << "-proc:none" }

dependencies {
    compile project(':moqui-util')

    // Groovy
    // NOTE: update attempt to 3.0.2 has issues with slow build (just slow stub generation? seems others are seeing that too)
    //     and updated Spock required (tried 2.0-M2-groovy-3.0, not final release and tests didn't work)
    compile 'org.codehaus.groovy:groovy:2.5.11' // Apache 2.0
    compile 'org.codehaus.groovy:groovy-dateutil:2.5.11' // Apache 2.0
    compile 'org.codehaus.groovy:groovy-json:2.5.11' // Apache 2.0
    compile 'org.codehaus.groovy:groovy-templates:2.5.11' // Apache 2.0
    compile 'org.codehaus.groovy:groovy-xml:2.5.11' // Apache 2.0
    // jansi is needed for groovydoc only, so in providedCompile (not included in war)
    compileOnly 'org.fusesource.jansi:jansi:1.18'
    // Findbugs need only during compile (used by freemarker and various moqui classes)
    compileOnly 'com.google.code.findbugs:annotations:3.0.1'

    // ========== Local (flatDir) libraries in framework/lib ==========

    // Bitronix Transaction Manager (the default internal tx mgr; custom build from source as 3.0.0 not yet released)
    compile 'org.codehaus.btm:btm:3.0.0-SNAPSHOT' // Apache 2.0
    runtime 'org.javassist:javassist:3.27.0-GA' // Apache 2.0

    // ========== Libraries from jcenter ==========

    // Apache Commons
    runtime 'commons-codec:commons-codec:1.14'
    compile 'org.apache.commons:commons-csv:1.8' // Apache 2.0
    // NOTE: commons-email depends on com.sun.mail:javax.mail, included below
    compile module('org.apache.commons:commons-email:1.5') // Apache 2.0
    compile 'org.apache.commons:commons-lang3:3.10' // Apache 2.0; used by cron-utils
    compile 'commons-beanutils:commons-beanutils:1.9.4' // Apache 2.0
    compile 'commons-collections:commons-collections:3.2.2' // Apache 2.0
    compile 'commons-digester:commons-digester:2.1' // Apache 2.0
    compile 'commons-fileupload:commons-fileupload:1.4' // Apache 2.0
    compile 'commons-io:commons-io:2.6' // Apache 2.0
    compile 'commons-logging:commons-logging:1.2' // Apache 2.0
    compile 'commons-validator:commons-validator:1.6' // Apache 2.0

    // Cron Utils
    compile 'com.cronutils:cron-utils:9.0.2' // Apache 2.0

    // Flexmark (markdown)
    compile 'com.vladsch.flexmark:flexmark:0.61.0'
    compile 'com.vladsch.flexmark:flexmark-ext-tables:0.61.0'
    compile 'com.vladsch.flexmark:flexmark-ext-toc:0.61.0'

    // Freemarker
    compile 'org.freemarker:freemarker:2.3.30' // Apache 2.0

    // Java Specifications
    compile 'javax.transaction:jta:1.1'
    compile 'javax.cache:cache-api:1.1.0'
    compile 'javax.jcr:jcr:2.0'
    // jaxb-api no longer included in Java 9 and later, also tested with openjdk-8
    compile module('javax.xml.bind:jaxb-api:2.3.1') // CDDL 1.1
    // NOTE: javax.activation:javax.activation-api is required by jaxb-api, has classes same as old 2012 javax.activation:activation used by javax.mail
    // NOTE: as of Java 11 the com.sun packages no longer available so for javax.mail need full javax.activation jar (also includes javax.activation-api)
    compile 'com.sun.activation:javax.activation:1.2.0' // CDDL 1.1
    // using websocket-api 1.0, don't update to 1.1 until used in Jetty, Tomcat, etc
    compile 'javax.websocket:javax.websocket-api:1.0'
    providedCompile 'javax.servlet:javax.servlet-api:3.1.0'
    // Specs not needed by default:
    // compile 'javax.resource:connector-api:1.5'
    // compile 'javax.jms:jms:1.1'

    // H2 Database
    compile 'com.h2database:h2:1.4.200' // MPL 2.0, EPL 1.0

    // Jackson Databind (JSON, etc)
    compile 'com.fasterxml.jackson.core:jackson-databind:2.10.3'

    // Jetty HTTP Client and Proxy Servlet
    compile 'org.eclipse.jetty:jetty-client:9.4.27.v20200227' // Apache 2.0
    compile 'org.eclipse.jetty:jetty-proxy:9.4.27.v20200227' // Apache 2.0
    // NOTE: update after version 9.4.18.v20190429 results in 'IllegalArgumentException: URI is not hierarchical' error, have workarounds for now catching exception in MClassLoader.getResources()

    // javax.mail
    // NOTE: javax.mail depends on 'javax.activation:activation' which is the old package for 'javax.activation:javax.activation-api' used by jaxb-api
    compile module('com.sun.mail:javax.mail:1.6.2') // CDDL

    // Joda Time (used by elasticsearch, aws)
    compile 'joda-time:joda-time:2.10.5' // Apache 2.0

    // JSoup (HTML parser, cleaner)
    compile 'org.jsoup:jsoup:1.13.1' // MIT

    // Apache Shiro
    compile module('org.apache.shiro:shiro-core:1.5.2') // Apache 2.0
    compile module('org.apache.shiro:shiro-web:1.5.2') // Apache 2.0

    // SLF4J, Log4j 2 (note Log4j 2 is used by various libraries, best not to replace it even if mostly possible with SLF4J)
    compile 'org.slf4j:slf4j-api:1.7.30'
    compile 'org.apache.logging.log4j:log4j-core:2.13.1'
    compile 'org.apache.logging.log4j:log4j-api:2.13.1'
    runtime 'org.apache.logging.log4j:log4j-slf4j-impl:2.13.1'
    runtime 'org.apache.logging.log4j:log4j-jcl:2.13.1'

    // SubEtha SMTP (module as depends on old javax.mail location; also uses SLF4J, activation included elsewhere)
    compile module('org.subethamail:subethasmtp:3.1.7')

    // Snake YAML
    compile 'org.yaml:snakeyaml:1.26' // Apache 2.0

    // Apache Jackrabbit - uncomment here or include elsewhere when Jackrabbit repository configurations are used
    // compile 'org.apache.jackrabbit:jackrabbit-jcr-rmi:2.12.1' // Apache 2.0
    // compile 'org.apache.jackrabbit:jackrabbit-jcr2dav:2.12.1' // Apache 2.0

    // Apache Commons JCS - Only needed when using JCSCacheToolFactory
    // compile 'org.apache.commons:commons-jcs-jcache:2.0-beta-1' // Apache 2.0

    // Liquibase (for future reference, not used yet)
    // compile 'org.liquibase:liquibase-core:3.4.2' // Apache 2.0

    // ========== test dependencies ==========
    // spock-core depends on groovy-all but we are including selected groovy modules, so don't get its dependencies
    testCompile module('org.spockframework:spock-core:1.3-groovy-2.5') // Apache 2.0
    testCompile 'junit:junit:4.13' // Apache 2.0
    testCompile 'org.hamcrest:hamcrest-core:2.2' // BSD 3-Clause

    // ========== executable war dependencies ==========
    // Jetty
    // NOTE: update to Jetty 9.4.25.v20191220 caused issue on response.getWriter().write(String): Internal error processing request: java.lang.IllegalStateException: s=OPEN,api=BLOCKED,sc=false,e=null
    execWarRuntime 'org.eclipse.jetty:jetty-server:9.4.27.v20200227' // Apache 2.0
    execWarRuntime 'org.eclipse.jetty:jetty-webapp:9.4.27.v20200227' // Apache 2.0
    execWarRuntime 'org.eclipse.jetty:jetty-jndi:9.4.27.v20200227' // Apache 2.0
    execWarRuntime module('org.eclipse.jetty.websocket:javax-websocket-server-impl:9.4.27.v20200227') // Apache 2.0
    execWarRuntime ('org.eclipse.jetty.websocket:javax-websocket-client-impl:9.4.27.v20200227') { // Apache 2.0
        exclude group: 'javax.websocket' } // we have the full websocket API, including the client one causes problems
    execWarRuntime 'javax.websocket:javax.websocket-api:1.0' // using 1.0, don't update to 1.1 until used in Jetty, Tomcat, etc
    execWarRuntime ('org.eclipse.jetty.websocket:websocket-server:9.4.27.v20200227') // Apache 2.0
    // only include this if using Endpoint and MessageHandler annotations:
    // execWarRuntime ('org.eclipse.jetty:jetty-annotations:9.4.27.v20200227') // Apache 2.0
}

// setup task dependencies to make sure the start sourceSets always get run
compileJava.dependsOn startClasses
compileTestGroovy.dependsOn classes
sourceSets.test.compileClasspath += files(sourceSets.main.output.classesDirs)

// by default the Java plugin runs test on build, change to not do that (only run test if explicit task)
// no longer works as of gradle 4.8 or possibly earlier, use clear() instead: check.dependsOn.remove(test)
check.dependsOn.clear()

test {
    dependsOn cleanTest
    include '**/*MoquiSuite.class'

    systemProperty 'moqui.runtime', '../runtime'
    systemProperty 'moqui.conf', 'conf/MoquiDevConf.xml'
    systemProperty 'moqui.init.static', 'true'

    testLogging.showStandardStreams = true; testLogging.showExceptions = true

    classpath += files(sourceSets.main.output.classesDirs); classpath += files(projectDir.absolutePath)
    // filter out classpath entries that don't exist (gradle adds a bunch of these), or ElasticSearch JarHell will blow up
    classpath = classpath.filter { it.exists() }

    beforeTest { descriptor -> logger.lifecycle("Running test: ${descriptor}") }
}

jar {
    // this is necessary otherwise jar won't build when war plugin is applied
    enabled = true
    archiveBaseName = 'moqui-framework'
    manifest { attributes 'Implementation-Title': 'Moqui Framework', 'Implementation-Version': version, 'Implementation-Vendor': 'Moqui Ecosystem' }
    from sourceSets.main.output
    // get all of the "resources" that are in component-standard directories instead of src/main/resources
    from fileTree(dir: projectDir.absolutePath, includes: ['data/**', 'entity/**', 'screen/**', 'service/**', 'template/**']) // 'xsd/**'
}

war {
    dependsOn jar
    // put the war file in the parent directory, ie the moqui dir instead of the framework dir
    destinationDirectory = projectDir.parentFile
    archiveFileName = 'moqui.war'
    // add MoquiInit.properties to the WEB-INF/classes dir for the deployed war mode of operation
    from(fileTree(dir: destinationDir, includes: ['MoquiInit.properties'])) { into 'WEB-INF/classes' }
    // this excludes the classes in sourceSets.main.output (better to have the jar file built above)
    classpath = configurations.runtime - configurations.providedCompile
    classpath file(jar.archivePath)

    // put start classes and Jetty jars in the root of the war file for the executable war/jar mode of operation
    from sourceSets.start.output
    from(files(configurations.execWarRuntime)) { into 'execlib' }
    // TODO some sort of config for Jetty? from file(projectDir.absolutePath + '/jetty/jetty.xml')
    // setup the manifest for the executable war/jar mode
    manifest { attributes 'Implementation-Title': 'Moqui Start', 'Implementation-Vendor': 'Moqui Ecosystem',
            'Implementation-Version': version, 'Main-Class': 'MoquiStart' }
}

task copyDependencies { doLast {
    delete file(projectDir.absolutePath + '/dependencies')
    copy { from configurations.runtime; into file(projectDir.absolutePath + '/dependencies') }
    copy { from configurations.testCompile; into file(projectDir.absolutePath + '/dependencies') }
} }
