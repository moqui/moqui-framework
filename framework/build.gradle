/*
 * This software is in the public domain under CC0 1.0 Universal plus a
 * Grant of Patent License.
 *
 * To the extent possible under law, the author(s) have dedicated all
 * copyright and related and neighboring rights to this software to the
 * public domain worldwide. This software is distributed without any
 * warranty.
 *
 * You should have received a copy of the CC0 Public Domain Dedication
 * along with this software (see the LICENSE.md file). If not, see
 * <http://creativecommons.org/publicdomain/zero/1.0/>.
 */

version = '3.1.0-rc2'

apply plugin: 'java-library'
apply plugin: 'groovy'
apply plugin: 'war'
// to run gradle-versions-plugin use "gradle dependencyUpdates"
apply plugin: 'com.github.ben-manes.versions'
// uncomment to add the Error Prone compiler; not enabled by default (doesn't work on Travis CI)
// apply plugin: 'net.ltgt.errorprone'
buildscript {
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.50.0'
        // uncomment to add the Error Prone compiler: classpath 'net.ltgt.gradle:gradle-errorprone-plugin:0.0.8'
    }
}
dependencyUpdates.resolutionStrategy { componentSelection { rules -> rules.all { ComponentSelection selection ->
    boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'b'].any { qualifier -> selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-].*/ }
    if (rejected) selection.reject('Release candidate')
} } }

// NOTE: packages should match group, name, version found at https://bintray.com/bintray/jcenter
repositories {
    flatDir name: 'localLib', dirs: projectDir.absolutePath + '/lib'
    mavenCentral()
}

sourceCompatibility = 11
targetCompatibility = 11
archivesBaseName = 'moqui'

sourceSets {
    start
    execWar
}

groovydoc {
    docTitle = "Moqui Framework ${version}"
    source = sourceSets.main.allSource
}

// tasks.withType(JavaCompile) { options.compilerArgs << "-Xlint:unchecked" }
// tasks.withType(JavaCompile) { options.compilerArgs << "-Xlint:deprecation" }
// tasks.withType(GroovyCompile) { options.compilerArgs << "-Xlint:unchecked" }
// tasks.withType(GroovyCompile) { options.compilerArgs << "-Xlint:deprecation" }

// Log4J has annotation processors, disable to avoid warning
tasks.withType(JavaCompile) { options.compilerArgs << "-proc:none" }
tasks.withType(GroovyCompile) { options.compilerArgs << "-proc:none" }

// NOTE: for dependency types and 'api' definition see: https://docs.gradle.org/current/userguide/java_library_plugin.html
dependencies {
    // Groovy
    // NOTE: Groovy 3.0.10-3.0.18 has a bug that somehow causes EntityDefinition.isViewEntity (public final boolean) to switch
    //     from true during constructor to false later on; see EntityFindBuilder.java:112-114 and EntityDefinition.groovy:50-53,94-95;
    //     for now using Boolean instead of boolean to resolve, but staying at 3.0.9 to avoid risk with other code
    api 'org.codehaus.groovy:groovy:3.0.19' // Apache 2.0
    api 'org.codehaus.groovy:groovy-dateutil:3.0.19' // Apache 2.0
    api 'org.codehaus.groovy:groovy-groovysh:3.0.19' // Apache 2.0
    // jline, an older version, is required by groovy-groovysh but not in its dependencies
    implementation 'jline:jline:2.14.6' // BSD
    api 'org.codehaus.groovy:groovy-json:3.0.19' // Apache 2.0
    api 'org.codehaus.groovy:groovy-templates:3.0.19' // Apache 2.0
    api 'org.codehaus.groovy:groovy-xml:3.0.19' // Apache 2.0
    // jansi is needed for groovydoc only, so in compileOnly (not included in war) - don't update to version 2, keep at version 1 for compatibility
    compileOnly 'org.fusesource.jansi:jansi:1.18'
    // Findbugs need only during compile (used by freemarker and various moqui classes)
    compileOnly 'com.google.code.findbugs:annotations:3.0.1'

    // ========== Local (flatDir) libraries in framework/lib ==========

    // Bitronix Transaction Manager (the default internal tx mgr; custom build from source as 3.0.0 not yet released)
    api 'org.codehaus.btm:btm:3.0.0-20161020' // Apache 2.0
    runtimeOnly 'org.javassist:javassist:3.29.2-GA' // Apache 2.0

    // ========== General Libraries from Maven Central ==========

    // Apache Commons
    api 'org.apache.commons:commons-csv:1.10.0' // Apache 2.0
    // NOTE: commons-email depends on com.sun.mail:javax.mail, included below, so use module() here to not get dependencies
    api module('org.apache.commons:commons-email:1.5') // Apache 2.0
    api 'org.apache.commons:commons-lang3:3.14.0' // Apache 2.0; used by cron-utils
    api 'commons-beanutils:commons-beanutils:1.9.4' // Apache 2.0
    api 'commons-codec:commons-codec:1.16.0' // Apache 2.0
    api 'commons-collections:commons-collections:3.2.2' // Apache 2.0
    api 'commons-digester:commons-digester:2.1' // Apache 2.0
    api 'commons-fileupload:commons-fileupload:1.5' // Apache 2.0
    api 'commons-io:commons-io:2.15.1' // Apache 2.0
    api 'commons-logging:commons-logging:1.3.0' // Apache 2.0
    api 'commons-validator:commons-validator:1.8.0' // Apache 2.0

    // Cron Utils
    api 'com.cronutils:cron-utils:9.2.1' // Apache 2.0

    // Flexmark (markdown)
    api 'com.vladsch.flexmark:flexmark:0.64.8'
    api 'com.vladsch.flexmark:flexmark-ext-tables:0.64.8'
    api 'com.vladsch.flexmark:flexmark-ext-toc:0.64.8'

    // Freemarker
    // Remember to change the version number in FtlTemplateRenderer and MNode class when upgrading
    api 'org.freemarker:freemarker:2.3.32' // Apache 2.0

    // H2 Database
    api 'com.h2database:h2:2.2.224' // MPL 2.0, EPL 1.0

    // Java Specifications
    api 'javax.transaction:jta:1.1'
    api 'javax.cache:cache-api:1.1.1'
    api 'javax.jcr:jcr:2.0'
    // jaxb-api no longer included in Java 9 and later, also tested with openjdk-8
    api module('javax.xml.bind:jaxb-api:2.3.1') // CDDL 1.1
    // NOTE: javax.activation:javax.activation-api is required by jaxb-api, has classes same as old 2012 javax.activation:activation used by javax.mail
    // NOTE: as of Java 11 the com.sun packages no longer available so for javax.mail need full javax.activation jar (also includes javax.activation-api)
    api 'com.sun.activation:javax.activation:1.2.0' // CDDL 1.1
    api 'javax.websocket:javax.websocket-api:1.1'
    // TODO: this should be compileOnlyApi, but that was not included in Gradle 5... so cannot have excluded from
    //     runtime in a single way for Gradle 5 and 7; for now leaving in api, not desirable because we don't want it in the war file
    api 'javax.servlet:javax.servlet-api:4.0.1'
    // Specs not needed by default:
    // api 'javax.resource:connector-api:1.5'
    // api 'javax.jms:jms:1.1'

    // Java TOTP
    api 'dev.samstevens.totp:totp:1.7.1' // MIT
    // dev.samstevens.totp:totp depends on com.google.zxing:javase which depends on com.beust:jcommander, but an older version with a CVE, so specify latest to fix
    api 'com.beust:jcommander:1.82'

    // Jackson Databind (JSON, etc)
    api 'com.fasterxml.jackson.core:jackson-databind:2.16.0'

    // Jetty HTTP Client and Proxy Servlet
    api 'org.eclipse.jetty:jetty-client:10.0.18' // Apache 2.0
    api 'org.eclipse.jetty:jetty-proxy:10.0.18' // Apache 2.0

    // javax.mail
    // NOTE: javax.mail depends on 'javax.activation:activation' which is the old package for 'javax.activation:javax.activation-api' used by jaxb-api
    api module('com.sun.mail:javax.mail:1.6.2') // CDDL

    // Joda Time (used by elasticsearch, aws)
    api 'joda-time:joda-time:2.12.5' // Apache 2.0

    // JSoup (HTML parser, cleaner)
    api 'org.jsoup:jsoup:1.17.1' // MIT

    // Apache Shiro
    api module('org.apache.shiro:shiro-core:1.13.0') // Apache 2.0
    api module('org.apache.shiro:shiro-web:1.13.0') // Apache 2.0

    // SLF4J, Log4j 2 (note Log4j 2 is used by various libraries, best not to replace it even if mostly possible with SLF4J)
    api 'org.slf4j:slf4j-api:2.0.9'
    implementation 'org.apache.logging.log4j:log4j-core:2.22.0'
    implementation 'org.apache.logging.log4j:log4j-api:2.22.0'
    runtimeOnly 'org.apache.logging.log4j:log4j-jcl:2.22.0'
    runtimeOnly 'org.apache.logging.log4j:log4j-slf4j2-impl:2.22.0'

    // SubEtha SMTP (module as depends on old javax.mail location; also uses SLF4J, activation included elsewhere)
    api module('org.subethamail:subethasmtp:3.1.7')

    // Snake YAML
    api 'org.yaml:snakeyaml:2.2' // Apache 2.0

    // Apache Jackrabbit - uncomment here or include elsewhere when Jackrabbit repository configurations are used
    // api 'org.apache.jackrabbit:jackrabbit-jcr-rmi:2.12.1' // Apache 2.0
    // api 'org.apache.jackrabbit:jackrabbit-jcr2dav:2.12.1' // Apache 2.0

    // Apache Commons JCS - Only needed when using JCSCacheToolFactory
    // api 'org.apache.commons:commons-jcs-jcache:2.0-beta-1' // Apache 2.0

    // Liquibase (for future reference, not used yet)
    // api 'org.liquibase:liquibase-core:3.4.2' // Apache 2.0

    // ========== test dependencies ==========

    // junit-platform-launcher is a dependency from spock-core, included explicitly to get more recent version as needed
    testImplementation 'org.junit.platform:junit-platform-launcher:1.10.1'
    // junit-platform-suite required for test suites to specify test class order, etc
    testImplementation 'org.junit.platform:junit-platform-suite:1.10.1'
    // junit-jupiter-api for using JUnit directly, not generally needed for Spock based tests
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.1'
    // Spock Framework
    testImplementation platform("org.spockframework:spock-bom:2.1-groovy-3.0") // Apache 2.0
    testImplementation 'org.spockframework:spock-core:2.1-groovy-3.0' // Apache 2.0
    testImplementation 'org.spockframework:spock-junit4:2.1-groovy-3.0' // Apache 2.0
    testImplementation 'org.hamcrest:hamcrest-core:2.2' // BSD 3-Clause

    // ========== executable war dependencies ==========
    // Jetty
    execWarRuntimeOnly 'org.eclipse.jetty:jetty-server:10.0.18' // Apache 2.0
    execWarRuntimeOnly 'org.eclipse.jetty:jetty-webapp:10.0.18' // Apache 2.0
    execWarRuntimeOnly 'org.eclipse.jetty:jetty-jndi:10.0.18' // Apache 2.0
    execWarRuntimeOnly 'org.eclipse.jetty.websocket:websocket-javax-server:10.0.18' // Apache 2.0
    execWarRuntimeOnly ('org.eclipse.jetty.websocket:websocket-javax-client:10.0.18') { // Apache 2.0
        exclude group: 'javax.websocket' } // we have the full websocket API, including the client one causes problems
    execWarRuntimeOnly 'javax.websocket:javax.websocket-api:1.1'
    execWarRuntimeOnly ('org.eclipse.jetty.websocket:websocket-jetty-server:10.0.18') // Apache 2.0
    // only include this if using Endpoint and MessageHandler annotations:
    // execWarRuntime ('org.eclipse.jetty:jetty-annotations:10.0.18') // Apache 2.0
    execWarRuntimeOnly 'org.apache.logging.log4j:log4j-slf4j18-impl:2.18.0'
}

// setup task dependencies to make sure the start sourceSets always get run
compileJava.dependsOn startClasses
compileTestGroovy.dependsOn classes
sourceSets.test.compileClasspath += files(sourceSets.main.output.classesDirs)

// by default the Java plugin runs test on build, change to not do that (only run test if explicit task)
// no longer works as of gradle 4.8 or possibly earlier, use clear() instead: check.dependsOn.remove(test)
check.dependsOn.clear()

test {
    useJUnitPlatform()
    testLogging { events "passed", "skipped", "failed" }
    testLogging.showStandardStreams = true; testLogging.showExceptions = true
    maxParallelForks 1

    dependsOn cleanTest
    include '**/*MoquiSuite.class'

    systemProperty 'moqui.runtime', '../runtime'
    systemProperty 'moqui.conf', 'conf/MoquiDevConf.xml'
    systemProperty 'moqui.init.static', 'true'

    classpath += files(sourceSets.main.output.classesDirs); classpath += files(projectDir.absolutePath)
    // filter out classpath entries that don't exist (gradle adds a bunch of these), or ElasticSearch JarHell will blow up
    classpath = classpath.filter { it.exists() }

    beforeTest { descriptor -> logger.lifecycle("Running test: ${descriptor}") }
}

jar {
    // this is necessary otherwise jar won't build when war plugin is applied
    enabled = true
    archiveBaseName = 'moqui-framework'
    manifest { attributes 'Implementation-Title': 'Moqui Framework', 'Implementation-Version': version, 'Implementation-Vendor': 'Moqui Ecosystem' }
    from sourceSets.main.output
    // get all of the "resources" that are in component-standard directories instead of src/main/resources
    from fileTree(dir: projectDir.absolutePath, includes: ['data/**', 'entity/**', 'screen/**', 'service/**', 'template/**']) // 'xsd/**'
}

war {
    dependsOn jar
    // put the war file in the parent directory, ie the moqui dir instead of the framework dir
    destinationDirectory = projectDir.parentFile
    archiveFileName = 'moqui.war'
    // add MoquiInit.properties to the WEB-INF/classes dir for the deployed war mode of operation
    from(fileTree(dir: destinationDir, includes: ['MoquiInit.properties'])) { into 'WEB-INF/classes' }
    // this excludes the classes in sourceSets.main.output (better to have the jar file built above)
    classpath = configurations.runtimeClasspath - configurations.providedCompile
    classpath file(jar.archivePath)

    // put start classes and Jetty jars in the root of the war file for the executable war/jar mode of operation
    from sourceSets.start.output
    from(files(configurations.execWarRuntimeClasspath)) { into 'execlib' }
    // TODO some sort of config for Jetty? from file(projectDir.absolutePath + '/jetty/jetty.xml')
    // setup the manifest for the executable war/jar mode
    manifest { attributes 'Implementation-Title': 'Moqui Start', 'Implementation-Vendor': 'Moqui Ecosystem',
            'Implementation-Version': version, 'Main-Class': 'MoquiStart' }
}

task copyDependencies { doLast {
    delete file(projectDir.absolutePath + '/dependencies')
    copy { from configurations.runtime; into file(projectDir.absolutePath + '/dependencies') }
    copy { from configurations.testCompile; into file(projectDir.absolutePath + '/dependencies') }
} }
